/*
 * Copyright 2021 JaaJSoft
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java-library'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id "maven-publish"
}

javafx {
    version = '11'
    modules = ['javafx.controls', 'javafx.fxml']
}

group 'dev.jaaj.fx'
version '0.3.0'

sourceCompatibility = 11

mainClassName = 'dev.jaaj.fx.core.Example'

repositories {
    mavenCentral()
    jcenter()
    maven {
        url  "https://nexus.jaaj.dev/repository/maven-public/"
    }
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    implementation 'org.jfxtras:jmetro:11.6.11'
    implementation 'net.java.dev.jna:jna-platform:5.5.0'
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.28'
    implementation 'dev.jaaj:event:0.1.9'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version
            from components.java
            pom {
                name = project.name
                description = 'A JavaFX library'
                url = 'https://github.com/JaaJSoft/JaaJFX-core'
                /*properties = [
                        myProp: "value",
                        "prop.with.dots": "anotherValue"
                ]*/
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    url = 'https://github.com/JaaJSoft/JaaJFX-core'
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username System.getenv('REPO_USER')
                password System.getenv('REPO_PASSWORD')
            }
            def releasesRepoUrl = "https://nexus.jaaj.dev/repository/maven-releases/"
            def snapshotsRepoUrl = "https://nexus.jaaj.dev/repository/maven-snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
